buildscript {
    // Lock plugin dependencies
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

plugins {
    // Needed for Forge+Fabric
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.11.0-SNAPSHOT" apply false
    // Kotlin
    id "org.jetbrains.kotlin.jvm" version "1.6.10" apply false
    // Kotlin linter
    id "org.jlleitschuh.gradle.ktlint" version "10.0.0"
    // Java linter
    id "checkstyle"
}

architectury {
    minecraft = rootProject.minecraft_version
}

// Lock dependencies
// https://docs.gradle.org/current/userguide/dependency_locking.html
dependencyLocking {
    lockAllConfigurations()
}


subprojects {
    apply plugin: "dev.architectury.loom"
    // Apply checkstyle and ktlint to check the code style of every sub project
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "checkstyle"
    apply plugin: "org.jetbrains.kotlin.jvm"

    repositories {
        maven {
            name = "Valkyrien Skies Internal"
            url = project.vs_maven_url
            content {
                // this repository *only* contains artifacts with group "org.valkyrienskies"
                includeGroup("org.valkyrienskies")
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}"
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.16.5+build.9:v2"
    }

    // configure checkstyle
    checkstyle {
        // configure to use checkstyle v8.41
        toolVersion "8.41"
        // Gradle should fail builds on checkstyle errors (not warnings)
        ignoreFailures = false
        // Checkstyle config file is in .checkstyle/checkstyle.xml
        configFile = file("${rootDir}/.checkstyle/checkstyle.xml")
    }

    // configure checkstyle, but different
    // https://docs.gradle.org/current/userguide/checkstyle_plugin.html
    tasks.withType(Checkstyle) {
        reports {
            // Do not output html reports
            html.enabled = false
            // Output xml reports
            xml.enabled = true
        }
    }

    // configure ktlint
    ktlint {
        reporters {
            // configure to output in checkstyle XML format
            reporter "checkstyle"
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    // Set the base name, version, and group to the values in the gradle.properties
    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
