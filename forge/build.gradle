plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "org.jetbrains.kotlin.jvm"
}

configurations {
    // Use shadow to shade the file but NOT add it as a dependency
    shadow
}

architectury {
    platformSetupLoomIde()
}

loom {
    mixinConfigs = ["examplemod-common.mixins.json", "examplemod-forge.mixins.json"]
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${rootProject.architect.minecraft}")
    mappings(minecraft.officialMojangMappings())
    forge("net.minecraftforge:forge:${rootProject.architect.minecraft}-${rootProject.forge_version}")

    compileOnly(project(path: ":common")) {
        transitive = false
    }
    runtimeOnly(project(path: ":common", configuration: "transformForgeFakeMod")) {
        transitive = false
    }
    shadow(project(path: ":common", configuration: "transformForge")) {
        transitive = false
    }

    shadow("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.21")
    shadow("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2")
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2")

    // vs-core
    implementation("com.github.ValkyrienSkies:vs-core:${rootProject.vs_core_version}") {
        exclude group: 'org.joml'
    }

    // JOML (TODO use dependency from vs-core instead)
    implementation("org.joml:joml:${rootProject.joml_version}")
    implementation("org.joml:joml-primitives:${rootProject.joml_version}")

    // Jackson Binary Dataformat for Object Serialization (TODO use dependency from vs-core instead)
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:${rootProject.jacksonVersion}")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:${rootProject.jacksonVersion}")
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadow]
    classifier "shadow"
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    archiveClassifier = "forge"
}